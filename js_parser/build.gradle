plugins {
    id "com.moowork.node" version "1.2.0"
}

dependencies {
    expectedBy project(":common_parser")
    expectedBy project(":common_nodes")
}

def nodeModules = project.getProjectDir().getPath() + "/node_modules/"

node {
    nodeModulesDir = file(nodeModules)
}

task populateNodeModules(type: Copy, dependsOn: [compileKotlin2Js, npmInstall]) {
    from compileKotlin2Js.destinationDir
    into nodeModules

    afterEvaluate {
        configurations.testCompile.each {
            if (it.absolutePath.endsWith(".jar")) {
                from zipTree(it.absolutePath).matching {
                    include '*.js'
                    include '*.js.map'
                }
            }
        }
    }
}

task runMocha(type: NodeTask, dependsOn: [compileTestKotlin2Js, populateNodeModules]) {
    script = file(nodeModules + '/mocha/bin/mocha')
    args = [compileTestKotlin2Js.outputFile]
}

test.dependsOn runMocha